/**
 * Root build configuration.
 * @author Tanemahuta
 */
plugins {
    id 'idea'
}
allprojects { prj ->

    prj.group = 'controllers.springer.springer'

    prj.buildscript.repositories {
        jcenter() // Use jcenter by default
        repositories {
            maven {
                name "typesafe-maven-release"
                url "https://repo.typesafe.com/typesafe/maven-releases"
            }
        }
    }

    prj.repositories {
        addAll(prj.buildscript.repositories)
        ivy {
            name "typesafe-ivy-release"
            url "https://repo.typesafe.com/typesafe/ivy-releases"
            layout "ivy"
        }
    }

    // This will apply the defaults (see buildscripts/*Defaults.gradle) for each project
    prj.plugins.whenPluginAdded { plg ->
        final shortPluginName = plg.class.simpleName.replaceAll(/Plugin$/, '')
        final defaultBuildscript = rootProject.file("buildscripts/${shortPluginName}Defaults.gradle")
        if (defaultBuildscript.exists()) {
            logger.info "Found default plugin configuration for {}, applying it: {}", prj, defaultBuildscript
            prj.apply from: defaultBuildscript
        }
    }
}


